def kustomize_prep(name:str, srcs:list, containers:list=[], params:dict=None, visibility:list=None, labels:list=[], deps:list=[]):
    # Make sure to canonicalise each container (they could be in the form of ':local_container').
    containers = [c for c in containers if c]
    containers = [canonicalise(container) for container in containers]

    # This will template image tags into k8s resource files that reference docker images by build target.
    fqns = [f'{c}_fqn' for c in containers]
    # Tag with appropriate labels
    labels += ['k8s'] + ['container:' + c for c in containers]

    # Now that we have a collection of files, each containing a container name + label, we can
    # create a multi-expression sed command to replace a build target with the actual containers.
    exports = [f'export {k}={v}' for k,v in params.items()] if params else []
    replacement_command = 'cat'
    envsubst_vars = ",".join(['${%s}' % k for k in params.keys()] if params else [])
    check_rule = None
    if containers:
        # Pseudo build rule to check that the specified containers exist in the k8s files.
        # Has to be a separate rule because containers might only occur in one of the files.
        check_rule = build_rule(
            name=name,
            tag='check',
            cmd='for IMG in %s; do grep "$IMG" $SRCS || (echo "Image $IMG not in k8s files"; exit 1); done' % ' '.join(containers),
            srcs=srcs,
        )

        # macos sed only supports posix regex expressions so escape sequences like \b don't work
        boundary_expr = "\\b" if CONFIG.OS != 'darwin' else ""
        subcommands = ' '.join([
            f'-e "s|{container}{boundary_expr}|$(cat $(location {fqn}))|g"'
            for container, fqn in sorted(zip(containers, fqns))
        ])
        replacement_command = f'sed {subcommands}'

    # Template each config YAML and collect them in the filegroup.
    rules = []
    for src in srcs:
        # For src_genrule_name
        cleaned_src = src.replace('/', '_').replace(':', '_')
        src_tag = cleaned_src.replace('.', '_')
        src_genrule_name = f'_{name}#{src_tag}'

        rules.append(f':{src_genrule_name}')
        genrule(
            name = src_genrule_name,
            srcs = [src],
            outs = [src],
            cmd = exports + [f"cat $SRCS | {replacement_command} | envsubst '{envsubst_vars}' > $OUT"],
            deps = fqns + [check_rule if check_rule else None],
        )

    files = filegroup(
        name = name,
        srcs = rules,
        visibility = visibility,
        labels = labels,
    )

    return files


def kustomize(name:str, srcs:list, namespace:str, overlay:str, kustomize_path:str, visibility:list=None, labels:list=[], deps:list=[]):
    # Generate a rule to build with kustomize
    package_name = package_name()
    genrule(
        name = f"{name}_build",
        cmd =  [
            f'kustomize build {package_name}/{kustomize_path}/overlays/{overlay} > $OUT'
        ],
        srcs = srcs,
        outs = [f'{name}_{overlay}.yaml'],
        deps = deps,
        binary = True
    )

    # Generate a rule to push the configs.
    sh_cmd(
        name = name + '_push',
        cmd = [f'kubectl apply -n {namespace} -f plz-out/bin/$(location :{name}_build)'],
        deps = [f":{name}_build"],
        labels = ["kustomize-push"],
    )

    # Generate a rule to cleanup the configs.
    sh_cmd(
        name = name + '_cleanup',
        cmd = [f'kubectl delete -n {namespace} -f plz-out/bin/$(location :{name}_build)'],
        deps = [f":{name}_build"],
        labels = ["kustomize-cleanup"],
    )
